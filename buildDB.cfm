<!DOCTYPE html>
<cfsetting requestTimeOut = "120">
<cfscript>
    function createDatabase() {
        var SQL = "";
        
        SQL =  "CREATE TABLE Beer (
                    id INT NOT NULL GENERATED BY DEFAULT AS IDENTITY CONSTRAINT Beer_PK PRIMARY KEY
                    ,breweryId INT 
                    ,name VARCHAR(100)
                    ,type VARCHAR(100) 
                    ,abv DEC(10,2) 
                    ,ibu INT 
                    ,brewery_name VARCHAR(100) 
                )";
        
        queryExecute( SQL );

        SQL =  "CREATE TABLE Brewery (
                    id INT NOT NULL GENERATED BY DEFAULT AS IDENTITY CONSTRAINT Brewery_PK PRIMARY KEY
                    ,name VARCHAR(100)
                    ,country VARCHAR(100) 
                    ,city VARCHAR(100)
                    ,state VARCHAR(100)
                )";

        queryExecute( SQL );

        var curPath = Replace( GetDirectoryFromPath( GetCurrentTemplatePath() ), "\", "/", "ALL");
        var importData = deserializeJSON( fileread( curPath & '/DB/sampleData.json', "utf-8" ) );
        var helperObj = new MVCAppFW1.model.services.helper();
        var breweryObj = new MVCAppFW1.model.services.brewery( helperObj );
        var beerObj = new MVCAppFW1.model.services.beer( helperObj );
        var breweries = [];
        var beers = [];
        var lastBeer = {};
        importData.each( function( e, i, a ) {
            // build an array of unique
            var breweryId = 0;
            var breweryArrayPos = breweries.find( 
                function( item ) {
                    return item.brewery_name == e.brewery_name && 
                        item.brewery_city == e.brewery_city;
            } );
            if ( !breweryArrayPos ) {
                breweryId = breweryObj.save( 0, e.brewery_name, e.brewery_city, 
                    e.brewery_state, e.brewery_country );
                breweries.append( {
                    "brewery_name": e.brewery_name,
                    "brewery_city": e.brewery_city,
                    "breweryId": breweryId
                } );
            } 
            else {
                breweryId = breweries[ breweryArrayPos ].breweryId;
            }
            variables.lastBreweryID = breweryId;
            if ( !beers.find(
                function( item ) {
                    return item.beer_name == e.beer_name &&
                        item.brewery_name == e.brewery_name;
                } ) ) {
                
                beers.append( {
                    "beer_name": e.beer_name,
                    "brewery_name": e.brewery_name
                } );
                lastBeer = {
                    "id": beers.len(),
                    "name": e.beer_name,
                    "breweryId": breweryId,
                    "beerType": e.beer_type,
                    "beerABV": e.beer_abv,
                    "beerIBU": e.beer_ibu
                };
                variables.lastBeerID = beerObj.save( 0, e.beer_name, breweryId,
                    e.beer_type, e.beer_abv, e.beer_ibu );
            }
        } );
    }
</cfscript>

<html lang="en">
    <head>
        <cfinclude template="/ProceduralApp/header.cfm">
        <title>Intro To MVC ColdFusion</title>
    </head>
    <body role="document">
        <div class="container" role="main">
            <div id="home" class="page-header">
                <ol class="breadcrumb">
                    <li class="active">
                        Home
                    </li>
                </ol>
                <h1>Intro To MVC in ColdFusion</h1>
            </div>
            <p>
                The database for this application is being constructed.  Please 
                wait a few seconds.
            </p>
            <i class="fa fa-cog fa-spin fa-3x fa-fw"></i>
            <span class="sr-only">Loading...</span>
        </div>
        <cfflush>
        <cfset createDatabase()>
        <script>
            location.reload(true);
        </script>
        <cfflush>
    </body>
</html>

